plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java-library'
}

// https://stackoverflow.com/a/61671513/13425151
bootJar {
    enabled = false
}
jar {
    enabled = true
}

description = 'ddi-beans'

dependencies {
    api 'org.apache.xmlbeans:xmlbeans:5.2.0'
    implementation 'org.apache.logging.log4j:log4j-core'
}

sourceSets {
    main {
        java {
            srcDirs= ["src/main/java"]
        }
        resources {
            srcDirs= [
                    "src/main/resources",
                    "src/main/ddi-sources"
            ]
        }
    }
}

task generateSources(type: JavaExec) {
    group 'build'
    description 'Generate java sources from DDI xsd'
    classpath = sourceSets.main.compileClasspath
    mainClass = 'org.apache.xmlbeans.impl.tool.SchemaCompiler'
    args = ['-srconly',
            '-src',
            sourceSets.main.java.sourceDirectories.asPath,
            '-d',
            sourceSets.main.resources.sourceDirectories[0].path,
            sourceSets.main.resources.sourceDirectories[1].path]
}

generateSources.configure {
    onlyIf {
        fileTree(sourceSets.main.resources.sourceDirectories[0]) {
            include '**/*.xsb'
        }.isEmpty()
    }
}

compileJava.dependsOn generateSources
