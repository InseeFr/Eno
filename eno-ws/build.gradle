plugins {
	id 'org.springframework.boot'
	id 'application'
	id 'io.spring.dependency-management'
	id 'java'
	id 'maven-publish'
	id 'jacoco'
}

ext {
   javaMainClass = 'fr.insee.eno.ws.EnoWsApplication'
}

application {
	getMainClass().set(javaMainClass)
}

// Remove the "-plain" jar
// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
tasks.jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	//
	implementation project(':eno-core')
	implementation project(':eno-treatments')
	// Lunatic
	implementation libs.lunatic.model
	implementation libs.pogues.model
	// Spring Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// Web Client
	// since Spring MVC RestTemplate class is deprecated, the webflux dependency is added to import WebClient
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	// Open API
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// Spring devtools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

configurations.configureEach {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == 'org.yaml' && details.requested.name == 'snakeyaml') {
			details.useVersion '2.2'
		} // security issue in versions <2.0 imported as transitive dependencies
	}
}

processResources {
	filesMatching('version.properties') {
		expand(project.properties)
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}
