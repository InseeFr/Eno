plugins {
	id 'org.springframework.boot'
	id 'application'
	id 'io.spring.dependency-management'
	id 'java'
	id 'maven-publish'
}

ext {
   javaMainClass = 'fr.insee.eno.ws.EnoWsApplication'
}

application {
	getMainClass().set(javaMainClass)
}

// Remove the "-plain" jar
// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
tasks.jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	//
	implementation project(':ddi-beans')
	implementation project(':eno-core')
	// Spring Web
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	// Open API // https://springdoc.org/v2/#spring-webflux-support
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.2.0'
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// Spring devtools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

processResources {
	filesMatching('version.properties') {
		expand(project.properties)
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}
